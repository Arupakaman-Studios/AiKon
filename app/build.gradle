plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

ext {
    versionCodeGPlayAds = 2
    versionCodeGPlayPro = 3
    versionCodeFdroid = 1
}

android {

    signingConfigs {

        debug {
            storeFile file('../aikon_debug_android.keystore')
        }
        release {
            def props = new Properties()

            def fileInputStream = new FileInputStream(file('../secure.properties'))
            props.load(fileInputStream)
            fileInputStream.close()

            storeFile = file(props['storeFilePath'])
            storePassword = props['storePassword']
            keyAlias = props['keyAlias']
            keyPassword = props['keyPassword']
        }

    }

    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.arupakaman.aikon"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode versionCodeGPlayAds
        versionName "1.0.1"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        //testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [appLabel: "AiKon"]

            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"

            manifestPlaceholders = [appLabel: "AiKon Dev"]

            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "variant"
    productFlavors {
        adsGPlay {
            dimension "variant"
            buildConfigField "boolean", "isFDroid", "false"
            buildConfigField "boolean", "isAdsOn", "true"
        }
        proGPlay {
            applicationId "com.arupakaman.aikon.donate"
            dimension "variant"
            buildConfigField "boolean", "isFDroid", "false"
            buildConfigField "boolean", "isAdsOn", "false"
        }
        fDroid {
            dimension "variant"
            buildConfigField "boolean", "isFDroid", "true"
            buildConfigField "boolean", "isAdsOn", "false"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        useIR = true
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding true
    }

    packagingOptions {
        exclude 'META-INF/*'
    }

    sourceSets {
        adsGPlay {
            res {
                srcDirs 'src/adsGPlay/res'
            }
            java {
                srcDirs 'src/adsGPlay/java'
            }
        }
        proGPlay {
            res {
                srcDirs 'src/proGPlay/res'
            }
            java {
                srcDirs 'src/proGPlay/java'
            }
        }
        fDroid {
            res {
                srcDirs 'src/fDroid/res'
            }
            java {
                srcDirs 'src/fDroid/java'
            }
        }
    }

}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.6.10"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation "androidx.multidex:multidex:2.0.1"

    implementation 'com.github.dhaval2404:colorpicker:2.0'

    /**
     *   With Google Play Dependencies
     */
    adsGPlayImplementation platform('com.google.firebase:firebase-bom:29.0.3')
    adsGPlayImplementation 'com.google.firebase:firebase-crashlytics-ktx'
    adsGPlayImplementation "com.google.firebase:firebase-messaging"
    adsGPlayImplementation 'com.google.android.play:core:1.10.3'
    adsGPlayImplementation 'com.google.firebase:firebase-ads:20.5.0'

    proGPlayImplementation platform('com.google.firebase:firebase-bom:29.0.3')
    proGPlayImplementation 'com.google.android.play:core:1.10.3'
    proGPlayImplementation 'com.google.firebase:firebase-crashlytics-ktx'
    proGPlayImplementation "com.google.firebase:firebase-messaging"

}